buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'kr.entree.spigradle' version '1.2.1'
    id 'java'
    id 'jacoco'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
}

apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/publish.gradle"

sourceCompatibility = 8
targetCompatibility = 8

ext {
    //Define one of the supported mc versions
    mcVersion = project.property("mcVersion")
    packageName = project.property("packageName")
    guavaVersion = '29.0-jre'
}

group project.property("group")

spigot {
    authors = [project.property("author")]
    apiVersion = project.property("apiVersion")
    load = STARTUP
    //    depends = ['']
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

archivesBaseName = project.property("pluginName")

repositories {
    mavenCentral()
    jcenter()

    //Added spigot repository
    maven {
        name = 'Spigot'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'

        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }

    maven { url = 'https://artifactory.silthus.net/artifactory/libs-release' }
    maven { url = 'https://artifactory.silthus.net/artifactory/libs-snapshot' }

    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://oss.sonatype.org/content/repositories/central' }

    //Add your repositories here
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url 'https://jitpack.io' }
}


dependencies {
    //Adds the spigot api to your plugin
    compileOnly "org.spigotmc:spigot-api:${mcVersion}-R0.1-SNAPSHOT"
    compileOnly 'org.projectlombok:lombok:1.18.12'
    compileOnly 'org.yaml:snakeyaml:1.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    //Add your dependencies here
    implementation 'net.silthus:configlib-bukkit:2.3.0'
    implementation 'co.aikar:acf-paper:0.5.0-SNAPSHOT'
    implementation 'com.github.stefvanschie.inventoryframework:IF:0.5.19'

    //Test dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
    testImplementation "org.mockito:mockito-core:3.+"
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.15:0.3.0-SNAPSHOT'
    testImplementation 'org.assertj:assertj-core:3.16.1'
    testImplementation 'org.projectlombok:lombok:1.18.12'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    ignoreFailures = true
}

shadowJar {
    classifier = ''
    dependencies {
        include(dependency('net.silthus:configlib-bukkit:2.3.0'))
        include(dependency('co.aikar:acf-paper:0.5.0-SNAPSHOT'))
        include(dependency('com.github.stefvanschie.inventoryframework:IF:0.5.19'))
    }
    relocate 'co.aikar.commands', "${packageName}.acf"
    relocate 'co.aikar.locales', "${packageName}.locales"
    relocate 'de.exlll.configlib', "${packageName}.configlib"
    relocate 'com.github.stefvanschie.inventoryframework', "${packageName}.inventoryframework"
}

tasks.build.dependsOn(shadowJar)
tasks.publish.dependsOn(shadowJar)

processResources {
    project.properties.put("version", this.version)
    expand project.properties
}

task cleanPlugin() {
    group 'deploy'
    description 'Cleans the plugin in the working directory up'
    delete 'working/plugins/' + project.property("pluginName") + '*.jar'
}

task copyPluginYaml(type: Copy, dependsOn: spigotPluginYaml) {
    from(new File(spigotPluginYaml.temporaryDir, "plugin.yml"))
    into(sourceSets.test.resources.srcDirs[0])
}

tasks.test.dependsOn(copyPluginYaml)

task deploy(type: Copy, dependsOn: [
        'cleanPlugin',
        'jar'
]) {
    group 'deploy'
    description 'Exports you plugin to the plugin directory in your test server'

    def pluginFolder = project.property('pluginFolder').toString()

    from shadowJar
    into pluginFolder.isEmpty() ? 'working/plugins' : pluginFolder
}

task setupServer {
    group 'server'
    description 'Downloads the server jar'

    def server = new File('working/server.jar')

    if (server.exists()) {
        server.delete()
    }

    def url = ''
    switch (mcVersion) {
        case '1.8.8':
            url = 'https://cdn.getbukkit.org/spigot/spigot-1.8.8-R0.1-SNAPSHOT-latest.jar'
            break

        case '1.12.2':
            url = 'https://cdn.getbukkit.org/spigot/spigot-1.12.2.jar'
            break

        case '1.13.2':
            url = 'https://cdn.getbukkit.org/spigot/spigot-1.13.2.jar'
            break

        case '1.14.4':
            url = 'https://cdn.getbukkit.org/spigot/spigot-1.14.4.jar'
            break
        case '1.15.2':
            url = 'https://cdn.getbukkit.org/spigot/spigot-1.15.2.jar'
            break
    }

    if (url.isEmpty()) {
        throw new GradleException('NOT A SUPPORTED VERSION, PLEASE USE [1.8.8, 1.12.2, 1.13.2, 1.14.4, 1.15.2]!')
    }

    new URL(url).withInputStream { i -> server.withOutputStream { it << i } }
}

defaultTasks 'build', 'deploy'
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
